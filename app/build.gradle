apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.tokend.template"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs("en", "ru", "uk")
        multiDexKeepFile file('multidex-keep.txt')

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            apply from: "app_config.gradle"
            if (file("dev_app_config.gradle").exists()) {
                apply from: "dev_app_config.gradle"
            }
            applyAppConfig(android.buildTypes.debug)

            signingConfig signingConfigs.debug
            debuggable true
        }

        QA {
            apply from: "app_config.gradle"
            applyAppConfig(android.buildTypes.QA)
            signingConfig signingConfigs.debug
            debuggable false
        }

        release {
            apply from: "app_config.gradle"
            if (file("release_app_config.gradle").exists()) {
                apply from: "release_app_config.gradle"
            }
            applyAppConfig(android.buildTypes.release)

            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode true
        preDexLibraries true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }
}

ext {
    room_version = '2.2.6'
    tokend_version = '3.7.1'
    tokend_rx_version = '2.4.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'com.google.android.gms:play-services-base:17.5.0'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation "org.tokend:sdk:$tokend_version"
    implementation "org.tokend:rx-sdk:$tokend_rx_version"

    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation 'com.journeyapps:zxing-android-embedded:3.6.0@aar'
    implementation 'com.google.zxing:core:3.4.1'
}